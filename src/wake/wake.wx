
Namespace wake

#Import "<std>"

#Import "wx"
#Import "findmsvc"
#Import "product"

#Import "newdocs/docsnode"
#Import "newdocs/docsbuffer"
#Import "newdocs/docsmaker"
#Import "newdocs/markdown"

Using wx.newdocs

#Import "geninfo/geninfo"

Using libc
Using std
Using std.json
Using wx..

Global opts_time:Bool

Global StartDir:String
Global WonkeyRootDir:String

Global profileName:String

Const TestArgs:="wake mods"

Const WX_FILE_EXT:=".wx"
Const MX2_FILE_EXT:=".monkey2"

'Const TestArgs:="wake app src/mx2cc/test.wx"

Function Main()
	
	'Set aside 64M for GC!
	GCSetTrigger( 64*1024*1024 )
	
	StartDir=CurrentDir()
	
	ChangeDir( AppDir() )
	
	Local env:="bin/"+HostOS+"/env_"+HostOS+".txt"
	
	While Not IsRootDir( CurrentDir() ) And GetFileType( env )<>FILETYPE_FILE
	
		ChangeDir( ExtractDir( StripSlashes( CurrentDir() ) ) )
	Wend
	
	If GetFileType( env )<>FILETYPE_FILE Fail( "Unable to locate wake 'bin' directory" )

	WonkeyRootDir = CurrentDir()

	CreateDir( "tmp" )
	
	Local args:String[]

#If __CONFIG__="debug"
'	Local tenv:="bin/env_"+HostOS+"_dev.txt"
'	If GetFileType( tenv )=FileType.File env=tenv
	args=TestArgs.Split( " " )
#else
	args=AppArgs()
#endif

	LoadEnv( env )
	
	Local moddirs:=New StringStack
	moddirs.Add( CurrentDir()+"modules/" )
	Local env_moduleDirs := GetEnv( "WX_MODULE_DIRS" ).Trim()
	' remove DoubleQuotes
	If env_moduleDirs.StartsWith("~q") And env_moduleDirs.EndsWith("~q")
		env_moduleDirs = env_moduleDirs.Slice(1,-1)
	Endif
	For Local moddir:=Eachin env_moduleDirs.Split( ";" )
		moddir=moddir.Replace( "\","/" )

		' Starts with Tilde? ($HOME)
		If moddir.StartsWith("~~/") Then moddir = UserDir() + moddir.Slice(2)

		moddir=RealPath( moddir )
		If Not moddir.EndsWith( "/" ) moddir+="/"
		If GetFileType( moddir )<>FileType.Directory Continue
		If Not moddirs.Contains( moddir ) moddirs.Add( moddir )
	Next
	Module.Dirs=moddirs.ToArray()
	
	If args.Length<2
		Print ""
		Print "wake " + WAKE_VERSION+WAKE_VERSION_EXT
		Print ""
		Print "Usage: "
		Print ""
		Print "    wake [action] [-options] [sources]"
		Print ""
		Print "Actions:"
		Print "  app              Make an application."
		Print "  mods             Make modules."
		Print "  docs             Make docs."
		Print "  version          Print wake version."
		Print ""
		Print "Options:"
		Print "  -quiet           Emit less info when building."
		Print "  -verbose=        Emit more info when building, one of: none, 0 to 3. (Defaults: 1 or -1 if quiet)."
		Print "  -clean           Force clean rebuild."
		Print "  -time            Output build time information."
		Print "  -parse           Parse only."
		Print "  -semant          Parse and semant."
		Print "  -translate       Parse, semant and translate."
		Print "  -build           Parse, semant, translate and build."
		Print "  -run             The works! The default."
		Print "  -apptype=        App type to make, one of: gui, console. (Defaults: gui). For web target, one of: wasm, asmjs, wasm+asmjs (Defaults: wasm)"
		Print "  -icon=           App icon. (macos/ios: .icns file, windows: .ico file)"
		Print "  -target=         Build target, one of: windows, macos, linux, raspbian, emscripten, android, ios, desktop. Desktop is an alias for current host. (Defaults: desktop)."
		Print "  -config=         Build config, one of: all, debug, release. (Defaults: 'all' for modules else 'debug')."
		Print "  -product=        Build product path. (default: sourceDir/sourceName.products/target/)"
		Print ""
		Print "Sources:"
		Print "  for make app     Single wonkey source file."
		Print "  for make mods    Space separated list of modules, or nothing to make all modules."
		Print "  for make docs    Space separated list of modules, or nothing to make all docs."
		Print ""

#If __DESKTOP_TARGET__
		If (Int( GetEnv( "WX_USE_MSVC" ) ) Or GetEnv( "WX_USE_MSVC" )="") and HostOS="windows"
			Print ""
			Print "wake: using 'cl' version:"
			Print ""
			Local arch:=GetEnv("WX_ARCH_WINDOWS").ToUpper()	
			system( "~q"+GetEnv( "WX_MSVC_PATH_"+arch )+"\cl.exe~q > tmp\_v.txt" )
			Print LoadString( "tmp/_v.txt" )
		Else
			Print ""
			Print "wake: using 'g++' version:"
			Print ""
			system( "g++ --version > tmp/_v.txt" )
			Print LoadString( "tmp/_v.txt" )
		Endif
#Endif
		exit_( 0 )
	Endif

	Local ok:=False

	Try
	
		Local cmd:=args[1]
		args=args.Slice( 2 )
		
		Local start:=std.time.Now()
		
		Select cmd
		Case "geninfo"
			ok=GenInfo( args )
		Case "app"
			ok=MakeApp( args )
		Case "mods"
			ok=MakeMods( args )
		Case "docs"
			ok=MakeDocs( args )
		Case "version"
			Print "wake " + WAKE_VERSION+WAKE_VERSION_EXT
			
		'********* TESTS *********
		Case "enummods"
			Print "Enum module dirs:"
			For Local dir:= Eachin EnumModules()
				Print "  "+dir
			Next
		'********* TESTS *********
		
		Default
			Fail( "Unrecognized wake command: '"+cmd+"'" )
		End
		
		Local elapsed:=std.time.Now()-start
		
		If opts_time Print "wake: elapsed time="+elapsed
		
	Catch ex:BuildEx
	
		Fail( "Internal wake build error" )
	End
	
	If Not ok libc.exit_( 1 )
End

Function GenInfo:Bool( args:String[] )

	Local opts:=New BuildOpts
	opts.productType="module"
	opts.target="desktop"
	opts.config="debug"
	opts.clean=False
	opts.fast=True
	opts.verbose=0
	opts.passes=2
	opts.geninfo=True
	
	args=ParseOpts( opts,args )
	If args.Length<>1 Fail( "Invalid app source file" )
		
	Local cd:=CurrentDir()
	ChangeDir( StartDir )
	opts.mainSource=RealPath( args[0].Replace( "\","/" ) )
	ChangeDir( cd )
	
	Print ""
	Print "***** Generating info for "+opts.mainSource+"' "+profileName+" *****"
	Print ""

	New BuilderInstance( opts )

	Local gen:=New GeninfoGenerator
	
	Builder.Parse()
	
	If opts.passes=1
		gen.GenSemantInfo()
'		gen.GenParseInfo()
	Else
		If Not Builder.errors.Length Builder.Semant()
		gen.GenSemantInfo()
	Endif
	
	Return Builder.errors.Length=0
End

Function MakeApp:Bool( args:String[] )

	Local opts:=New BuildOpts
	opts.productType="app"
	opts.target="desktop"
	opts.config="debug"
	opts.clean=False
	opts.fast=True
	opts.verbose=0
	opts.passes=5
	
	args=ParseOpts( opts,args )
	
	If args.Length<>1 Fail( "Invalid app source file" )
	
	Local cd:=CurrentDir()
	ChangeDir( StartDir )
	
	'DebugStop()
	
	Local srcPath:=RealPath( args[0].Replace( "\","/" ) )

	If Not GetFileType(srcPath) = FileType.File
		Fail("Can't find source file '"+srcPath+"'")
	Endif

	If opts.appIcon Then opts.appIcon = RealPath( opts.appIcon.Replace( "\","/" ) )

	Local product := New Product(WonkeyRootDir, srcPath, opts)
	product.CheckPath() ' set default products path

	ChangeDir( cd )
	
	opts.mainSource=srcPath
	
	Print ""
	Print "***** Making app '"+opts.mainSource+"' "+profileName+" *****"
	Print ""

	New BuilderInstance( opts )

	'pass1 	
	Builder.Parse()
	If opts.passes=1 Return Builder.errors.Length=0
	If Builder.errors.Length Return False
	
	Builder.Semant()
	If opts.passes=2 Return Builder.errors.Length=0
	If Builder.errors.Length Return False

	product.PrepareFolder() ' not required before Parse() and Semant()

	Builder.Translate()
	If opts.passes=3 Return Builder.errors.Length=0
	If Builder.errors.Length Return False
	
	Builder.product.Build()
	If Builder.errors.Length Return False
	product.CopyAndFinalize() ' final product preparation after successful build
	Select opts.target
		Case "ios"        Print "Application built: "+opts.product+product.appName+".xcodeproj"
		Case "android"    Print "Application built: "+opts.product
		Case "emscripten" Print "Application built: "+StripExt(opts.product)+".html"
		Default           Print "Application built: "+opts.product
	End Select
	If opts.passes=4
		Return True
	Endif
	
	Builder.product.Run()
	Return True
End

Function MakeMods:Bool( args:String[] )

	Local opts:=New BuildOpts
	opts.productType="module"
	opts.target="desktop"
	opts.config="debug,release" 'all
	opts.clean=False
	opts.fast=True
	opts.verbose=0
	opts.passes=4

	args=ParseOpts( opts,args )

	If Not args Then args=EnumModules() Else args=OrderModuleDependancies( args, True )
	
	Local errs:=0
	
	Local target:=opts.target
	
	Local configs:= opts.config.Split(",")
	
	For Local config:=Eachin configs
		
		opts.config = config
	
		profileName="("+opts.target+" "+opts.config+" "+opts.arch+" "+opts.toolchain+(opts.threads ? " mx" Else "")+")"
		
		For Local modid:=Eachin args
			
			Local path:=""
			For Local moddir:=Eachin Module.Dirs
				For Local ext:=Eachin New String[](WX_FILE_EXT, MX2_FILE_EXT)
					path=moddir+modid+"/"+modid+ext
					If GetFileType( path )=FileType.File Exit
					path=""
				Next
				If path Then Exit
			Next
			If Not path Fail( "Module '"+modid+"' not found" )
		
			Print ""
			Print "***** Making module '"+modid+"' "+profileName+" *****"
			Print ""
			
			opts.mainSource=RealPath( path )
			opts.target=target
			
			New BuilderInstance( opts )
			
			Builder.Parse()
			If Builder.errors.Length errs+=1;Continue
			If opts.passes=1 Continue
	
			Builder.Semant()
			If Builder.errors.Length errs+=1;Continue
			If opts.passes=2 Continue
			
			Builder.Translate()
			If Builder.errors.Length errs+=1;Continue
			If opts.passes=3 Continue
			
			Builder.product.Build()
			If Builder.errors.Length errs+=1;Continue
		Next
	
	Next
	
	Return errs=0
End

Function MakeDocs:Bool( args:String[] )

	Local opts:=New BuildOpts
	opts.productType="module"
	opts.target="desktop"
	opts.config="debug"
	opts.clean=False
	opts.fast=True
	opts.verbose=0
	opts.passes=2
	opts.makedocs=true
	
	args=ParseOpts( opts,args )
	
	opts.clean=False
	
	If Not args
		args=EnumModules()
		DeleteDir( "docs/modules",True )
	Endif

	Local docsDir:=RealPath( "docs" )+"/"
	
	Local pageTemplate:=LoadString( "docs/new_docs_page_template.html" )
	
	Local docsMaker:=New DocsMaker( docsDir,pageTemplate )

	Local errs:=0
	
	For Local modid:=Eachin args
		
		Local path:=""
		For Local moddir:=Eachin Module.Dirs
			For Local ext:=Eachin New String[](WX_FILE_EXT, MX2_FILE_EXT)
				path=moddir+modid+"/"+modid+ext
				If GetFileType( path )=FileType.File Exit
				path=""
			Next
			If path Then Exit
		Next
		If Not path Fail( "Module '"+modid+"' not found" )
	
		Print ""
		Print "***** Doccing module '"+modid+"' *****"
		Print ""
		
		opts.mainSource=RealPath( path )
		
		New BuilderInstance( opts )

		Builder.Parse()
		If Builder.errors.Length errs+=1;Continue
		
		Builder.Semant()
		If Builder.errors.Length errs+=1;Continue

		Local module:=Builder.modules.Top
		
		docsMaker.CreateModuleDocs( module )
		
	Next
	
	Local buf:=New StringStack
	Local modsbuf:=New StringStack
	
	For Local modid:=Eachin EnumModules()

		Local index:=LoadString( "docs/modules/"+modid+"/manual/index.js" )
		If index and Not index.Trim() Print "module OOPS modid="+modid
		If index buf.Push( index )
		
		index=LoadString( "docs/modules/"+modid+"/module/index.js" )
		If index and Not index.Trim() Print "manual OOPS modid="+modid
		If index modsbuf.Push( index )
	Next
	
	buf.Add( "{text:'Modules reference',children:[~n"+modsbuf.Join( "," )+"]}~n" )
	
	Local tree:=buf.Join( "," )
	
	Local page:=LoadString( "docs/new_docs_template.html" )
	page=page.Replace( "${DOCS_TREE}",tree )
	SaveString( page,"docs/newdocs.html" )
	
	Print "~nMake docs complete."
	
	Return True
End

Function ParseOpts:String[]( opts:BuildOpts,args:String[] )

	Global done:=False
	Assert( Not done )
	done=True
	
	opts.verbose=Int( GetEnv( "WX_VERBOSE" ) )
	
	Local i:=0
	
	For i=0 Until args.Length
	
		Local arg:=args[i]
		
		' Options without parameters
		Select arg
		Case "-run"
			opts.passes=5
		Case "-build"
			opts.passes=4
		Case "-translate"
			opts.passes=3
		Case "-semant"
			opts.passes=2
		Case "-parse"
			opts.passes=1
		Case "-clean"
			opts.clean=True
		Case "-quiet"
			opts.verbose=-1
		Case "-verbose"
			opts.verbose=1
		Case "-time"
			opts_time=True
		Default
			If arg.StartsWith( "-" )
				' Options with parameters
				Local j:=arg.Find( "=" )
				
				If j=-1 Fail( "Expected value for option '"+arg+"'" )
					
				Local opt:=arg.Slice( 0,j ),val:=arg.Slice( j+1 )
				
				Local path:=val.Replace( "\","/" )
				If path.StartsWith( "~q" ) And path.EndsWith( "~q" ) path=path.Slice( 1,-1 )
				
				val=val.ToLower()
				
				Select opt
				Case "-product"
					opts.product=path
				Case "-icon"
					opts.appIcon=path
				Case "-apptype"
					opts.appType=val
				Case "-target"
					Select val
					Case "desktop","windows","macos","linux","raspbian","emscripten","android","ios"
						opts.target=val
					Default
						Fail( "Invalid value for 'target' option: '"+val+"' - must be 'desktop', 'raspbian', 'emscripten', 'android' or 'ios'" )
					End
				Case "-config"
					Select val
					Case "all"
						opts.config="debug,release"
					Case "debug","release"
						opts.config=val
					Default
						Fail( "Invalid value for 'config' option: '"+val+"' - must be 'debug' or 'release'" )
					End
				Case "-verbose"
					Select val
					Case "0","1","2","3","-1"
						opts.verbose=Int( val )
					Default
						Fail( "Invalid value for 'verbose' option: '"+val+"' - must be '0', '1', '2', '3' or '-1'" )
					End
				Default
					Fail( "Unrecognized option '"+arg+"'" )
				End
			Else
				' Path to wonkey file
				Exit
			Endif
		End
	Next
	args=args.Slice( i )
	
	If Not opts.target Or opts.target="desktop" opts.target=HostOS
		
	opts.wholeArchive=Int( GetEnv( "WX_WHOLE_ARCHIVE" ) )
		
	opts.threads=Int( GetEnv( "WX_THREADS" ) )
		
	opts.toolchain="gcc"
	
	Select opts.target
	Case "windows"
		
		If Not opts.appType opts.appType="gui"
		
		opts.arch=GetEnv( "WX_ARCH_"+opts.target.ToUpper(),"x64" )
		
		If opts.arch<>"x64" And opts.arch<>"x86"
			Fail( "Unrecognized architecture '"+opts.arch+"'" )
		Endif
		
'		Print "WX_USE_MSVC='"+GetEnv( "WX_USE_MSVC" )+"'"
		
		Local msvc:=GetEnv( "WX_USE_MSVC" )
		
		If msvc=""
			msvc=FindMSVC() ? "1" Else "0"
		Endif
		
		If Int( msvc )
			
			opts.toolchain="msvc"
			
			Local arch:=opts.arch.ToUpper()
			
			SetEnv( "PATH",GetEnv( "WX_MSVC_PATH_"+arch )+";"+GetEnv( "PATH" ) )
			SetEnv( "INCLUDE",GetEnv( "WX_MSVC_INCLUDE_"+arch ) )
			SetEnv( "LIB",GetEnv( "WX_MSVC_LIB_"+arch ) )
			
		Endif
	
		If opts.arch="x64" And opts.toolchain<>"msvc"
			'Fail( "x64 builds for windows currently only supported for msvc" )
		Endif
		
	Case "linux"
		
		If Not opts.appType opts.appType="gui"

		opts.arch="x64"

	Case "macos"
		
		If Not opts.appType opts.appType="gui"

		local macos_arch:=GetEnv("WX_MACOS_ARCHS")

		If macos_arch.Contains("-arch x86_64")
			opts.arch="x64"
		Elseif macos_arch.Contains("-arch arm64")
			opts.arch="arm64"
		Else
			opts.arch="x64"
		End
		
	Case "raspbian"

		If Not opts.appType opts.appType="gui"
			
		opts.arch="arm32"
			
		SetEnv( "PATH",GetEnv( "WX_RASPBIAN_TOOLS" )+";"+GetEnv( "PATH" ) )
		
	Case "emscripten"
		
		If Not opts.appType opts.appType="wasm"
			
		opts.arch="llvm"
		
		opts.threads=False
		
	Case "android"
		
		opts.arch=GetEnv( "WX_ANDROID_APP_ABI","armeabi-v7a" )
		
	Case "ios"
		
		local ios_archs:=GetEnv( "WX_IOS_ARCHS" )

		if ios_archs.Contains(" armv7 ")
			opts.arch="armv7"
		elseif ios_archs.Contains(" armv7s ")
			opts.arch="armv7s"
		elseif ios_archs.Contains(" arm64 ")
			opts.arch="arm64"
		elseif ios_archs.Contains(" arm64e ")
			opts.arch="arm64e"
		elseif ios_archs.Contains(" i386 ") and Int( GetEnv( "WX_IOS_USE_SIMULATOR" ) )
			opts.arch="x86"
		elseif ios_archs.Contains(" x86_64 ") and Int( GetEnv( "WX_IOS_USE_SIMULATOR" ) )
			opts.arch="x64"
		else
			opts.arch="arm64"
			If Int( GetEnv( "WX_IOS_USE_SIMULATOR" ) ) opts.arch="x64"
		end
		
	Default
		
		Fail( "Unrecognized target '"+opts.target+"'" )
	
	End
	
	Select opts.appType
	Case "console","gui"
		
		Select opts.target
		Case "windows","macos","linux","raspbian"
		Default
			Fail( "apptype '"+opts.appType+"' is only valid for desktop targets" )
		End
		
	case "wasm","asmjs","wasm+asmjs"
		
		If opts.target<>"emscripten" Fail( "apptype '"+opts.appType+"' is only valid for emscripten target" )
			
	case ""
	Default
		Fail( "Unrecognized apptype '"+opts.appType+"'" )
	End
	
	profileName="("+opts.target+" "+opts.config+" "+opts.arch+" "+opts.toolchain+(opts.threads ? " mx" Else "")+")"
		
	Return args
End

Function EnumModules( out:StringStack,cur:String,src:String,deps:StringMap<StringStack> )
	
	If Not deps.Contains( cur )
		Print "Can't find module dependancy '"+cur+"' - check module.json file for '"+src+"'"
		Return
	End
	
	If out.Contains( cur ) Return
	
	For Local dep:=Eachin deps[cur]
		EnumModules( out,dep,cur,deps )
	Next
	
	out.Push( cur )
End

Function EnumModules:String[]( modsArray:String[] = Null )

	Local mods:=New StringMap<StringStack>

	For Local moddir:=Eachin Module.Dirs
		
		For Local f:=Eachin LoadDir( moddir )
		
			Local dir:=moddir+f+"/"
			If GetFileType( dir )<>FileType.Directory Continue
			
			' If modsArray is given, we use only modules within modsArray.
			' Used for enum + sorting specific modules when using:
			' wake mods mod1 mod2 mod3'.
			'
			If modsArray
				Local found:Bool = False
				For Local modname := Eachin modsArray
					If modname = f Then found = True; Exit
				Next
				If Not found Then Continue
			Endif
			
			Local str:=LoadString( dir+"module.json" )
			If Not str Continue
			
			Local obj:=JsonObject.Parse( str )
			If Not obj
				Print "Error parsing json:"+dir+"module.json"
				Continue
			Endif
			
			Local name:=obj["module"].ToString()
			If name<>f Continue
			
			Local deps:=New StringStack
			If name<>"wonkey" deps.Push( "wonkey" )
			
			Local jdeps:=obj["depends"]
			If jdeps
				For Local dep:=Eachin jdeps.ToArray()
					deps.Push( dep.ToString() )
				Next
			Endif
			
			mods[name]=deps
		Next
	
	Next
	
	Local out:=New StringStack
	For Local cur:=Eachin mods.Keys
		EnumModules( out,cur,"",mods )
	Next
	
	Return out.ToArray()
End

Function OrderModuleDependancies:String[]( modules:String[], sorted:Bool=True )
	Local modsArr := New StringStack( modules ) ' input modules
	Local modsArrNew := New StringStack         ' input modules + dependancies
	
	For Local moddir:=Eachin Module.Dirs
		For Local f:=Eachin LoadDir( moddir )
		
			Local dir:=moddir+f+"/"
			If GetFileType( dir )<>FileType.Directory Continue
			
			' check only modules within modsArr
			If Not modsArr.Contains(f) Then Continue
			
			Local str:=LoadString( dir+"module.json" )
			If Not str Continue
			
			Local obj:=JsonObject.Parse( str )
			If Not obj
				Print "Error parsing json:"+dir+"module.json"
				Continue
			Endif
			
			Local name:=obj["module"].ToString()
			If name<>f Continue

			If Not modsArrNew.Contains( name ) Then modsArrNew.Add( name )

			Local jdeps:=obj["depends"]
			If jdeps
				For Local dep:=Eachin jdeps.ToArray()
					Local depName := dep.ToString()
					If Not modsArrNew.Contains( depName )
						Local additionalDeps := OrderModuleDependancies( New String[](depName), False )
						For Local additionalDepName := Eachin additionalDeps
							If Not modsArrNew.Contains( additionalDepName ) Then modsArrNew.Add( additionalDepName )
						Next
					Endif
				Next
			Endif
			
 		Next
	Next
	
	' Always add module "wonkey"
	If Not modsArrNew.Contains( "wonkey" ) Then modsArrNew.Add( "wonkey" )

	If sorted=False Then Return modsArrNew.ToArray()

	' Sort everything by dependancy
	modsArrNew = New StringStack( EnumModules( modsArrNew.ToArray() ) )

	Local outArr := New StringStack

	For Local module := Eachin modsArrNew
		If modsArr.Contains( module )
			outArr.Push( module )
		Endif
	Next

	Return outArr.ToArray()
End

Function LoadEnv:Bool( path:String )

	SetEnv( "WX_HOME",CurrentDir() )
	SetEnv( "WX_MODULES",CurrentDir()+"modules" )

	Local lineid:=0
	
	For Local line:=Eachin stringio.LoadString( path ).Split( "~n" )
		lineid+=1
		
		line=line.Trim()
		If Not line Or line.StartsWith( "'" ) Or line.StartsWith( "#" ) Continue
	
		Local i:=line.Find( "=" )
		If i=-1 Fail( "Env config file error at line "+lineid )
		
		Local name:=line.Slice( 0,i ).Trim()
		Local value:=line.Slice( i+1 ).Trim()
		
		value=ReplaceEnv( value,lineid )
		
		SetEnv( name,value )

	Next
	
	Return True
End

Function ReplaceEnv:String( str:String,lineid:Int )
	Local i0:=0
	Repeat
		Local i1:=str.Find( "${",i0 )
		If i1=-1 Return str
		
		Local i2:=str.Find( "}",i1+2 )
		If i2=-1 Fail( "Env config file error at line "+lineid )
		
		Local name:=str.Slice( i1+2,i2 ).Trim()
		
		Local value:=GetEnv( name )
		
		If Not value
			
			Select name
			Case "WX_IOS_SDK"
				
				Local sdk:="iphoneos"
				
				If Int( GetEnv( "WX_IOS_USE_SIMULATOR" ) ) sdk="iphonesimulator"
		
				system( "xcrun --sdk "+sdk+" --show-sdk-path >tmp/_p.txt" )
				
				value=LoadString( "tmp/_p.txt" ).Trim()
				
				SetEnv( "WX_IOS_SDK",value )
				
			Case "WX_IOS_ARCHS"
				
				value="-arch armv7 -arch arm64"
				
				If Int( GetEnv( "WX_IOS_USE_SIMULATOR" ) ) value="-arch x86_64"
			End
			
			If value SetEnv( name,value )
			
		Endif
		
		str=str.Slice( 0,i1 )+value+str.Slice( i2+1 )
		i0=i1+value.Length
		
	Forever
	Return ""
End

Function Fail( msg:String )

	Print ""
	Print "wake: "+(String.FromChar(27)+"[31m")+"fatal error"+(String.FromChar(27)+"[0m")+": " + msg
	Print ""

	Print String.FromChar(7) 'Ringbell
		
	exit_( 1 )
End
